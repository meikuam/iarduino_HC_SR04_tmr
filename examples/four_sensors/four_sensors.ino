// Бибилиотека разработана для ультразвукового датчика расстояния:
// HC-SR04 http://iarduino.ru/shop/Sensory-Datchiki/ultrazvukovogo-datchika-hc-sr04-rasstoyaniya-dvizheniya.html
// Вывод TRIG датчика HC_SR04 подключается к любому выводу Arduino (указывется первым при объявлении объекта).
// Вывод ECHO датчика HC_SR04 подключается к любому выводу Arduino (указывется вторым при объявлении объекта).

#include <iarduino_HC_SR04_tmr.h>                        // Подключаем библиотеку для работы с датчиком.
iarduino_HC_SR04_tmr hc[4]={{12,11},{10,9},{8,7},{6,5}}; // Объявляем массив состоящий из 4 объектов для работы с 4 датчиками, указав номера выводов TRIG и ECHO каждого датчика.
                                                         // (1 датчик: TRIG=12, ECHO=11) (2 датчик: TRIG=10, ECHO=9) (3 датчик: TRIG=8, ECHO=7) (4 датчик: TRIG=6, ECHO=5).
void setup(){                                            //
     for(int i=0; i<4; i++){ hc[i].begin(100); }         // Инициируем работу с каждым из четырёх датчиков указав период их опроса по 100 мс.
     Serial.begin(9600);                                 // Инициируем работу с шиной UART на скорости 9600 бит/сек, для передачи данных в монитор последовательного порта.
}                                                        //
                                                         //
void loop(){                                             //
//   Включаем и отключаем датчики:                       //
     int j = millis()%200;                               // Определяем переменную j которая будет принимать значения от 0 до 199 увеличивающиеся с каждой миллисекундой.
     for(int i=0; i<4; i++){ hc[i].work( i==(j/50) ); }  // Поочерёдно включаем 1 из 4 датчиков на 50 мс, отключив 3 остальных.
                                                         //
//   Выводим показания всех датчиков:                    //
     if( millis()%1000==0 ){                             // Условие оператора if будет выполняться 1 раз за 1000 мс (1 раз в секунду).
         for(int i=0; i<4; i++){                         //
             Serial.println(hc[i].distance());           // Выводим расстояние до препятствия 1 датчика (расстояние не сбрасывается если датчик отключён).
         }   Serial.println("============");             // 
         delay(2);                                       // Ждём 2 мс, чтоб не вывести данные несколько раз за 1 секунду.
     }                                                   //
}                                                        //

// Метод объект.distance() может принимать в качестве параметра температуру воздуха в °C (от -128 до +127) для увеличения точности измерений (по умолчанию +23°C).
// Метод объект.begin()    в данном примере принимает период опроса 100 мс, значит датчик не будет опрошен более 1 раза за время его включения методом объект.work().
// Метод объект.work()     в данном примере поочерёдно включает датчики на 50 мс, так что одновременно работает только один датчик.
//                         Таким образом звуковая волна отправленная одним датчиком не будет принята другим датчиком и не исказит его показания.